# Maintainer: Gerard Ribugent <ribugent <at> gmail <dot> com>
pkgname=python-nile-git
_name=nile
pkgver=r36.fc95890
pkgrel=1
pkgdesc=" Unofficial Amazon Games client"
arch=(any)
url="https://github.com/imLinguin/nile"
license=('GPL3')
depends=(
	'python>=3.8'
	'python-requests>=2.27.1' "python-requests<3.0.0"
	'python-pyqt5>=5.15.6'
	'python-pyqt5-webengine>=5.15.5' 'python-pyqt5-webengine<6.0.0'
	'python-protobuf'
	'python-pycryptodome>=3.14.1' 'python-pycryptodome<4.0.0'
	'python-zstandard>=0.17.0' 'python-zstandard<1.0.0'
)
makedepends=(python-build python-installer python-wheel)
source=(
	"$_name::git+https://github.com/imLinguin/nile.git#branch=main"
	'pyproject.toml'
)
sha512sums=('SKIP'
            '8a6a02f72e29676ccb203c59fb7e2802833f3f8e09ae6b95f1d4d578fe864e15bdcbb340ec4f69851a30240fee930d413220ce7a99d24c72be2ade72741e4a93')

pkgver() {
	cd "$_name"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cp pyproject.toml "$_name"
	# mkdir "$_name/src"
	# mv "$_name/nile" "$_name/src"
}

build() {
	cd "$_name"
	python -m build --wheel --no-isolation
}

package() {
	cd "$_name"
	python -m installer --destdir="$pkgdir" dist/*.whl
	install -Dm0644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
